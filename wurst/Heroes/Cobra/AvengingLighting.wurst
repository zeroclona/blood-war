package AvengingLighting

import HeroEntity
import CobraConsts
import SpellIds
import Spell
import SoundUtils
import ClosureEvents
import ClosureTimers

class LightningEffect
  private lightning lgt
  private unit first
  private unit second

  construct(unit f, unit s)
    first = f
    second = s
    lgt = addLightning(LIGHTNING_FINGER_OF_DEATH, false, first.getPos3Fly(), second.getPos3Fly())
    ..setColor(Cobra.AvengingLgt.lgtcolor)
    doPeriodicallyTimed(Cobra.AvengingLgt.lgtmoveperiod, Cobra.AvengingLgt.lgtmovetime) j ->
      lgt.move(false, first.getPos3Real(), second.getPos3Real())
    doAfter(Cobra.AvengingLgt.lgtmovetime) ->
      destroy this

  ondestroy
    lgt.destr()

public class AvengingLighting extends Spell
  static let dmg = Cobra.AvengingLgt.damage
  static let enemycount = Cobra.AvengingLgt.addenemycount
  static let enemyradius = Cobra.AvengingLgt.enemytakeradius
  let s = new SoundDefinition(Cobra.AvengingLgt.impactsound, false)
  unit filterunit

  construct()
    super(SpellIds.avenginglighting)
    EventListener.onTargetCast(id, (source, target) -> onSpellEffect(source, target))

  function impact(unit source, unit target, real damage) // source, target, damage
    damageTarget(source, target, damage, DAMAGE_TYPE_MAGIC)
    s.playOnPoint(target.getPos3Real(), 1)
    new LightningEffect(source, target)

  function targetFilter() returns bool
    return stdEnemyTargetCheck(filterunit, GetFilterUnit())


  function onSpellEffect(unit source, unit target)
    print(source.getName()+" casts an Avenging Lightning")
    let lvl = source.getAbilityLevel(id)
    let damage = dmg.run(lvl, source.getSpellPower())
    filterunit = source
    var g = CreateGroup()..enumUnitsInRange(target.getPos(), enemyradius.run(lvl), Filter(function stdTargetFilter))..removeUnit(target)
    filterunit = null

    impact(source, target, damage)
    for int i = 0 to enemycount.run(lvl) + 1
      var maxdist = REAL_MAX
      unit nearest = null
      for unit u in g
        let distSq = target.getPos().distanceToSq(u.getPos())
        if distSq < maxdist
          nearest = u
          maxdist = distSq
      g.removeUnit(nearest)
      impact(source, nearest, damage)
    g.destr()


//try to change targets.addunit into 58-61 (to get out a group)


init
  new AvengingLighting()
